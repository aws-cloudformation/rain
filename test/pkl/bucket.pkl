// Represents a CloudFormation template that creates buckets
// in several different ways, with and without the use of modules

amends "modules/template.pkl"
import "modules/cloudformation.pkl" as cfn
import "modules/aws/s3/bucket.pkl" as bucket
import "modules/patterns/bucket.pkl" as pattern

Description = "Create a bucket"

Metadata { 
    ["Foo"] = "bar"
}

Parameters {
    ["Name"] {
        Type = "String"
        Default = "baz"
    }
}

local compliantBucket = new pattern {
    LogicalId = "CompliantBucket"
    AppName = "my-app"
}

Resources {
    ["UntypedBucket"] {
        Type = "AWS::S3::Bucket"
        Properties {
           ["BucketName"] {
                ["Ref"] = "Name"
           }
        }
    }

    ["TypedBucket1"] = new bucket.Bucket {
        BucketName = "foo"
    }

    ["TypedBucket2"] = new bucket.Bucket {
        BucketName = cfn.Ref("Name")
    }

    for (name in List("bucketx", "buckety", "bucketz")) {
        [name] = new bucket.Bucket {
            BucketName = name
        }
    }

    // Import multiple resources from a pattern module 
    for (logicalId, resource in compliantBucket.Resources) {
        [logicalId] = (resource) {
            // This adds a Metadata key to each Resource in the pattern
            // It leaves existing keys alone
            Metadata {
                ["TestAmendingObjects..."] = "Test"
            }
        }
    }

}

Outputs {
    ["BucketZName"] {
        Value = cfn.Ref("bucketz")
    }
}

output {
    renderer = new YamlRenderer {}
}


